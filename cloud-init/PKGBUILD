#
# Upstream maintainer:  kpcyrd <git@rxv.cc>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Patches from: Indie Computing Corp.
#

maintainer="http://indiecomputing.com/"
pkgname=cloud-init
pkgver=18.2
pkgrel=2
pkgdesc="The standard for customising cloud instances"
arch=('any')
url="https://cloud-init.io"
license=("GPL3")
depends=('systemd' 'sudo' 'python2-yaml' 'python2-cheetah' 'python2-prettytable'
         'python2-oauth' 'python2-boto' 'python2-configobj'
         'python2-jsonschema' 'python2-jsonpatch' 'python2-jsonpointer' 'net-tools'
         'python2-requests' 'python2-argparse' 'python2-oauthlib'
         'python2-jinja')
makedepends=('python2' 'python2-setuptools')
backup=(etc/cloud/cloud.cfg etc/cloud/cloud.cfg.d/05_logging.cfg)
source=("https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        fix-lib.patch
        ubos.cloud.cfg)
sha512sums=('25ccfbd41d46f1375e14094754a92930d589bca3d7f0f016c55d3475751aba4b8c8474fc6aadd48521c68606d9f02f96aa8cd46644f71ba9af958612c4dae55c'
            '6c435fbe1e0a7093b79f8bb3789b05a39afd1e43b7ff96f13e68c9ad16311cc58270b947bae574cfdc3d2e27c7258789880a101c9bdf2c3c141bcbedc47a8a4b'
            '29ddf1c6e44c81e9e0004a3dc32a85d3a44206ce305a46a38ca40d652435466fd2ec5bd53f69a2ecf8b496b1b6317f19aa7a972384b0678f73e941a094bc61d7')

releasepage=('https://launchpad.net/cloud-init')
pkgverforked=('18.2-1')

prepare(){
  cd $pkgname-$pkgver

  patch -Np1 -i ../fix-lib.patch

  find . -name \*.py -exec sed -i '1s/python$/&2/' {} +
  sed -i '1s/python$/&2/' tools/read*

  sed -e 's:/etc/systemd:/usr/lib/systemd:g' -e 's:\"/lib\":\"/usr/lib\":g' -i setup.py

  patch -Np1 -i "${startdir}/fix-logging.patch"
}

package() {
  cd $pkgname-$pkgver

  python2 ./setup.py install --root="$pkgdir" --init-system systemd

  # Use a cloud.cfg crafted for UBOS
  install -m644 ../ubos.cloud.cfg "$pkgdir"/etc/cloud/cloud.cfg
}
