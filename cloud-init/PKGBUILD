# Maintainer: Christian Rebischke <chris.rebischke at archlinux.org>
# Contributor:  kpcyrd <git@rxv.cc>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: flaccid aka Chris Fordham <chris@fordham.id.au>
# Contributor: Sparadox <etienne.lafarge at gmail.com>
# Patches from: Indie Computing Corp.

maintainer="http://indiecomputing.com/"
pkgname=cloud-init
pkgver=19.3
pkgrel=2
pkgdesc="The standard for customising cloud instances"
arch=('any')
url="https://cloud-init.io"
license=("GPL3")
depends=('systemd' 'sudo' 'python-yaml' 'python-prettytable'
         'python-oauthlib' 'python-configobj'
         'python-jsonschema' 'python-jsonpatch' 'net-tools'
         'python-requests' 'python-jinja' 'python-six'
         'netplan')
makedepends=('python' 'python-setuptools')
backup=(etc/cloud/cloud.cfg etc/cloud/cloud.cfg.d/05_logging.cfg)
source=("https://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        "fix-lib.patch"
        "ubos.cloud.cfg")
sha512sums=('4b2c7dbcfeeaa9abc097e4f89035009fe839347041ab93cf33593bf31add3f6e6744cf8cd080de4d937b7e1aa50026f109c8f9240452dbff331b4d477d558e2d'
            '6c435fbe1e0a7093b79f8bb3789b05a39afd1e43b7ff96f13e68c9ad16311cc58270b947bae574cfdc3d2e27c7258789880a101c9bdf2c3c141bcbedc47a8a4b'
            '5046d9b65782c05e04e084aed8dbe52bff7eb71f3316f6ad9586a6ac629335ca8afe07d21c5e42616635f194cbd1170820add4d2ba3680ee520190f0039e8cf3')

releasepage=('https://git.archlinux.org/svntogit/community.git/tree/trunk?h=packages/cloud-init')
pkgverforked=('19.3-1')

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-lib.patch"
  sed -e 's:/etc/systemd:/usr/lib/systemd:g' -e 's:\"/lib\":\"/usr/lib\":g' -i setup.py

  patch -Np1 -i "${startdir}/fix-logging.patch"
}

package() {
  cd "${pkgname}-${pkgver}"

  python ./setup.py install --root="${pkgdir}" --init-system systemd

  # Use a cloud.cfg crafted for UBOS
  install -m644 ../ubos.cloud.cfg "$pkgdir"/etc/cloud/cloud.cfg
}
