# Maintainer: Christian Rebischke <chris.rebischke at archlinux.org>
# Contributor:  kpcyrd <git@rxv.cc>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: flaccid aka Chris Fordham <chris@fordham.id.au>
# Contributor: Sparadox <etienne.lafarge at gmail.com>
# Patches from: Indie Computing Corp.

maintainer="http://indiecomputing.com/"
pkgname=cloud-init
pkgver=21.2
pkgrel=1
pkgdesc="The standard for customising cloud instances"
arch=('any')
url="https://cloud-init.io"
license=("GPL3")
depends=('dhclient' 'netplan' 'python-configobj' 'python-jinja'
         'python-jsonpatch' 'python-jsonschema' 'python-oauthlib' 'python-requests'
         'python-yaml' 'sudo' 'systemd')
makedepends=('python' 'python-setuptools')
checkdepends=('python-httpretty' 'python-prettytable' 'python-pytest')
optdepends=('cloud-guest-utils')
backup=(etc/cloud/cloud.cfg etc/cloud/cloud.cfg.d/05_logging.cfg)
source=("https://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        "fix-lib.patch"
        "ubos.cloud.cfg")
sha512sums=('8cc24945efc6f16b3c64411c5e7e5b83582d337f1c3f546163d52c14a6177ff752e91ba1fec04b7ade800f921c2b79c04d032df62c2b4171930ca1fb74c49a7d'
            '6c435fbe1e0a7093b79f8bb3789b05a39afd1e43b7ff96f13e68c9ad16311cc58270b947bae574cfdc3d2e27c7258789880a101c9bdf2c3c141bcbedc47a8a4b'
            '326b95d75c80aa8b342db26ecc2379921fcc11b3b2e5ddd5d4f9a906dfc241a795c286aa5f9aac4d799e543736b174064377743ab465f9c96b9b13ea99195310')

releasepage=('https://git.archlinux.org/svntogit/community.git/tree/trunk?h=packages/cloud-init')
pkgverforked=('21.2-1')

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-lib.patch"
  # sed -e 's:/etc/systemd:/usr/lib/systemd:g' -e 's:\"/lib\":\"/usr/lib\":g' -i setup.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"

  python ./setup.py install --skip-build \
    --optimize=1 \
    --init-system systemd \
    --root="${pkgdir}"

  # Use a cloud.cfg crafted for UBOS
  install -m644 ../ubos.cloud.cfg "$pkgdir"/etc/cloud/cloud.cfg
}
