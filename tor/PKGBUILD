# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne
# From Arch, using UBOS configuration

pkgname=tor
pkgver=0.4.8.10
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('x86_64')
url='https://www.torproject.org/download/tor/'
license=("BSD-3-Clause" "LGPL-3.0-only" "MIT")
depends=(
  'bash'
  'libcap.so'
  'libcrypto.so'
  'libevent'
  'liblzma.so'
  'libseccomp.so'
  'libssl.so'
  'libsystemd.so'
  'libz.so'
  'libzstd.so'
)
optdepends=('torsocks: for torify')
makedepends=('ca-certificates' 'systemd')
backup=('etc/tor/torrc')
source=("https://dist.torproject.org/${pkgname}-${pkgver}.tar.gz"{,.sha256sum{,.asc}}
        # 'torrc.patch'
        'tor.sysusers'
        'tor.tmpfiles'
        'tor.service')
sha512sums=('f5cb32f5d0fc333aa1607370e290e6be85230896bd26e00072a68db4b32293d5dd38cccb57e92941fbe6ac17fb4207c38d11e3095ede289c3a6ebbd64ed70119'
            'SKIP'
            '45bdf0a3afa93b0e48bd4a3ae7702a571041d9ac761035a07505436be7fd6d89d5b4ed046fc5a5a75a3119c6fcb8a1e42b05e7638925ac8b63ff0780461851ee'
            '262a38979f42a4a675149e058410db4165ed97f8544f2d95838983a1ec9c760332c5857ab182e60c5c44104cd3ac551959a26a80036826c18c1e93e9e387584c'
            '430b660feaa00b9b2fb0a2a5b42ebfad1c56252031632fd297f1bf7de1857367fe293233715b7c941b9859895df0ce541f3b5426a393e4d75c1f89f311202233')
validpgpkeys=('2133BC600AB133E1D826D173FE43009C4607B1FB'  # Nick Mathewson
              'F65CE37F04BA5B360AE6EE17C218525819F78451') # Roger Dingledine

releasepage=('https://archlinux.org/packages/extra/x86_64/tor/')
pkgverforked=('0.4.8.10-1')

prepare() {
  # verify the signed sums match the expected source tarball
  sha256sum -c ${pkgname}-${pkgver}.tar.gz.sha256sum
  # cd ${pkgname}-${pkgver}
  # uncomment essential config sections in the torrc file
  # patch -Np1 < "${srcdir}/torrc.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/ubos \
    --enable-gpl
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  mkdir -p -m755 ${pkgdir}/etc/tor/sites.d
  install -Dm0644 ${startdir}/{pre,post}fix -t ${pkgdir}/etc/tor/

  install -Dm0644 "${srcdir}/*.service" -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm0644 "${srcdir}/tor.sysusers" "${pkgdir}/usr/lib/sysusers.d/tor.conf"
  install -Dm0644 "${srcdir}/tor.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/tor.conf"

  install -Dm0755 "${startdir}/bin/recreate-torrc" "${pkgdir}/usr/share/tor/bin/recreate-torrc"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
