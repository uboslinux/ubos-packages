# From Arch, using UBOS configuration

pkgname=tor
pkgver=0.4.2.5
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('x86_64')
url='https://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'bash' 'libseccomp')
optdepends=('torsocks: for torify')
makedepends=('ca-certificates')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist')
source=("https://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        # 'torrc'
        'tor.service'
        'tor.sysusers'
        'tor.tmpfiles')
sha512sums=('e63157713caa3590324f652f17c5c5634bab015dcde5c7832eb725351c3a91c1051dd8a95650502703e9c3dd5daefc2867ff3d20530dfec17310bb3dd4bb5b7d'
            'SKIP'
            '430b660feaa00b9b2fb0a2a5b42ebfad1c56252031632fd297f1bf7de1857367fe293233715b7c941b9859895df0ce541f3b5426a393e4d75c1f89f311202233'
            '45bdf0a3afa93b0e48bd4a3ae7702a571041d9ac761035a07505436be7fd6d89d5b4ed046fc5a5a75a3119c6fcb8a1e42b05e7638925ac8b63ff0780461851ee'
            '262a38979f42a4a675149e058410db4165ed97f8544f2d95838983a1ec9c760332c5857ab182e60c5c44104cd3ac551959a26a80036826c18c1e93e9e387584c')
validpgpkeys=('2133BC600AB133E1D826D173FE43009C4607B1FB'  # Nick Mathewson
              'F65CE37F04BA5B360AE6EE17C218525819F78451') # Roger Dingledine

releasepage=('https://git.archlinux.org/svntogit/community.git/tree/trunk?h=packages/tor')
pkgverforked=('0.3.4.9-1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/ubos
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  mkdir -p -m755 ${pkgdir}/etc/tor/sites.d
  install -Dm0644 ${startdir}/{pre,post}fix -t ${pkgdir}/etc/tor/

  install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"
  install -Dm0644 "${srcdir}/tor.sysusers" "${pkgdir}/usr/lib/sysusers.d/tor.conf"
  install -Dm0644 "${srcdir}/tor.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/tor.conf"

  install -Dm0755 "${startdir}/bin/recreate-torrc" "${pkgdir}/usr/share/tor/bin/recreate-torrc"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
