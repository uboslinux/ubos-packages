developer="https://pagekite.org/"
maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
pkgver=0.5.9.3
pkgrel=2
pkgdesc='localhost tunneling via pagekite.net, integrated into ubos-admin'
arch=('any')
url=${developer}
license=('AGPL2')
depends=('python2' 'python2-setuptools' 'python2-socksipychain' 'ubos-admin')
source=("pagekite-${pkgver}.tar.gz::https://github.com/pagekite/PyPagekite/archive/v${pkgver}.tar.gz")
releasepage=('https://github.com/pagekite/PyPagekite/releases')
sha512sums=('4a35849e5fa00423a64c5d781c5f2079fd07e06b36e318a609326f9baa8d14a12d2fe4d0671acf758431b91e4793c411e8e3f63e683a9561736277d033c0119d')

_vendor_perl=$(perl -V::vendorarch: | sed -e "s![' ]!!g")

package() {
# Pagekite code
    cd "${srcdir}/PyPagekite-${pkgver}"
    python2 setup.py install --root="${pkgdir}/" --optimize=1

# Config
    mkdir -m0755 -p ${pkgdir}/etc/pagekite.d
    install -D -m0644 ${startdir}/etc/pagekite.d/*              -t ${pkgdir}/etc/pagekite.d/
    install -D -m0644 ${startdir}/etc/ubos/hostname-callbacks/* -t ${pkgdir}/etc/ubos/hostname-callbacks/

# Systemd
    install -D -m0644 ${startdir}/systemd/*.service -t ${pkgdir}/usr/lib/systemd/system/

# User
    install -D -m0644 ${startdir}/sysusers/* -t ${pkgdir}/usr/lib/sysusers.d/

# Code
    install -D -m0755 ${startdir}/vendor_perl/UBOS/Commands/*.pm          -t ${pkgdir}${_vendor_perl}/UBOS/Commands/
    install -D -m0755 ${startdir}/vendor_perl/UBOS/HostnameCallbacks/*.pm -t ${pkgdir}${_vendor_perl}/UBOS/HostnameCallbacks/
    install -D -m0755 ${startdir}/vendor_perl/UBOS/Pagekite/*.pm          -t ${pkgdir}${_vendor_perl}/UBOS/Pagekite/
}
