developer="https://pagekite.org/"
maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
pkgver=1.5.2.201011
pkgrel=1
pkgdesc='localhost tunneling via pagekite.net, integrated into ubos-admin'
arch=('any')
url=${developer}
license=('AGPL2')
depends=('python' 'python-setuptools' 'python-socksipychain' 'ubos-admin')
source=("pagekite-${pkgver}.tar.gz::https://github.com/pagekite/PyPagekite/archive/v${pkgver}.tar.gz")
releasepage=('https://github.com/pagekite/PyPagekite/releases')
sha512sums=('094f03fc35974c2b5e6b7e704ea8aa0163c3b49c21cfdda2a883f959b2473fc962d08588391c787b7a7ed7bf9f1eec7c695ad5826c378484ac4fc2c946831051')

_vendor_perl=/usr/share/perl5/vendor_perl


package() {
# Pagekite code
    cd "${srcdir}/PyPagekite-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1

# Config
    mkdir -m0755 -p ${pkgdir}/etc/pagekite.d
    install -D -m0644 ${startdir}/etc/pagekite.d/*              -t ${pkgdir}/etc/pagekite.d/
    install -D -m0644 ${startdir}/etc/ubos/hostname-callbacks/* -t ${pkgdir}/etc/ubos/hostname-callbacks/

# Systemd
    install -D -m0644 ${startdir}/systemd/*.service -t ${pkgdir}/usr/lib/systemd/system/

# User
    install -D -m0644 ${startdir}/sysusers/* -t ${pkgdir}/usr/lib/sysusers.d/

# Code
    install -D -m0755 ${startdir}/vendor_perl/UBOS/Commands/*.pm          -t ${pkgdir}${_vendor_perl}/UBOS/Commands/
    install -D -m0755 ${startdir}/vendor_perl/UBOS/HostnameCallbacks/*.pm -t ${pkgdir}${_vendor_perl}/UBOS/HostnameCallbacks/
    install -D -m0755 ${startdir}/vendor_perl/UBOS/Pagekite/*.pm          -t ${pkgdir}${_vendor_perl}/UBOS/Pagekite/
}
