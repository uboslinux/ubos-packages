developer=https://storj.io/
url=https://github.com/storj/storj
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.31.12
pkgrel=3
pkgdesc="Storj V3 Network"
arch=('x86_64' 'armv6l' 'armv7l' 'aarch64')
license=("AGPL3")
depends=(
    'aws-cli'
)

sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP') # skip for now; needs platform-specific checksums
case "$(uname -m)" in
"x86_64")
     _platform=amd64
    ;;
"armv6l" | "armv7l" )
    _platform=arm
    ;;
"aarch64")
    _platform=arm64
    ;;
*)
    echo "ERROR: Unsupported platform" >&2; exit 1
    ;;
esac

source=(
    "gateway_linux_${_platform}_${pkgver}.zip::https://github.com/storj/storj/releases/download/v${pkgver}/gateway_linux_${_platform}.zip"
    "identity_linux_${_platform}_${pkgver}.zip::https://github.com/storj/storj/releases/download/v${pkgver}/identity_linux_${_platform}.zip"
    "linksharing_linux_${_platform}_${pkgver}.zip::https://github.com/storj/storj/releases/download/v${pkgver}/linksharing_linux_${_platform}.zip"
    "uplink_linux_${_platform}_${pkgver}.zip::https://github.com/storj/storj/releases/download/v${pkgver}/uplink_linux_${_platform}.zip"
)
_vendor_perl=/usr/share/perl5/vendor_perl
releasepage=('https://github.com/storj/storj/releases/latest')


package() {
    # code
    install -m0755 -D ${srcdir}/{gateway,identity,linksharing,uplink}_linux_${_platform} -t ${pkgdir}/usr/bin/
    for f in gateway identity linksharing uplink; do
        cat <<END > ${pkgdir}/usr/bin/storj-$f
#!/bin/bash
# Invoke Storj executable with default arguments for UBOS
#

/usr/bin/${f}_linux_${_platform} --config-dir /etc/storj \$@
END
        chmod 755 ${pkgdir}/usr/bin/storj-$f
    done
    install -m0755 -D ${startdir}/bin/storj-gateway-credentials -t ${pkgdir}/usr/bin/

    # systemd
    install -m0644 -D ${startdir}/systemd/*.service -t ${pkgdir}/usr/lib/systemd/system/

    # config
    mkdir -m0755 -p ${pkgdir}/etc/storj

    # UBOS DataTransferProtocol
    install -D -m755 ${startdir}/vendor_perl/UBOS/DataTransferProtocols/*.pm -t ${pkgdir}${_vendor_perl}/UBOS/DataTransferProtocols/

    # For data
    mkdir -m0755 -p ${pkgdir}/ubos/lib/storj/{identity,storage}
}

