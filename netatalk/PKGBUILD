# UBOS note: This is currently not in Arch, only in AUR, so we maintain it ourselves.
# So far, there are no changes to upstream.
# https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=netatalk
#
# Maintainer: Denis Yantarev <denis dot yantarev at gmail dot com>
# Contributor: SJ_UnderWater
# Contributor: Dominik Dingel <mail at wodar dot de>
# Contributor: William Udovich <nerdzrule7 at earthlink dot net>
# Contributor: Farhan Yousaf <farhany at xaviya dot com>

maintainer="http://indiecomputing.com/"
pkgname=netatalk
pkgver=3.1.12
pkgrel=1
pkgdesc='Open-source implementation of the Apple Filing Protocol'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://netatalk.sourceforge.net'
license=('GPL')
depends=('avahi>=0.6' 'libldap' 'libgcrypt>=1.2.3' 'libevent' 'python2' 'dbus-glib' 'python2-dbus' 'pam')
replaces=('netatalk-git' 'netatalk2')
backup=('etc/afp.conf'
    'etc/extmap.conf')
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2
    avahi-client.patch
    python2.patch)
# releasepage=('http://netatalk.sourceforge.net/')
releasepage=('https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=netatalk')
sha512sums=('0b22abaf0084d5684ccc049225abb34caa32893ea8fe7eb67901fde94726f054149ce78d2da49cf06fe30360eb18b2edccff20f9ac69d5ab5105f6d2f4178759'
            '61ffecb2962ad5ada8d73658956ea25e13e04a40dd901bbb91fed14eaa39728b37c12c5f78c32fdf91b074618ee8aa9b46d9babf2692fdaa2da9f9b26c730585'
            '1c53f057ce19afcec15e3efa76aad310dc89bf14be7779c59066fd7389744198ae0008ea17c9ca23c1d5be93a8f5bbe1772c7da56fbce99a276260d59bb9f42a')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -p0 < "$srcdir/python2.patch"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    autoreconf -fi
    CFLAGS="-Wno-unused-result -O2" \
        ./configure --prefix=/usr --localstatedir=/var/state --sysconfdir=/etc \
        --sbindir=/usr/bin --enable-silent-rules --with-init-style=systemd \
        --with-cracklib --with-cnid-cdb-backend --enable-pgp-uam --with-libevent=no
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
