# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>
# Forked for UBOS
# Using our own .service files

pkgverforked=('1.6.21-1')
releasepage=('https://gitlab.archlinux.org/archlinux/packaging/packages/memcached')

pkgname=memcached
pkgver=1.6.21
pkgrel=1
pkgdesc='Distributed memory object caching system'
url='https://memcached.org/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'libevent' 'libseccomp' 'libseccomp.so')
optdepends=('perl: for memcached-tool usage')
source=(https://www.memcached.org/files/${pkgname}-${pkgver}.tar.gz
        # memcached.service.patch
        memcached.service  # added
        memcached@.service # added
        memcached.tmpfiles
        memcached.sysusers
        0001-tests-enable-drop_privileges-by-default-for-better-s.patch
        0001-seccomp-extend-syscalls-tested-on-Linux-6.1-with-gli.patch)
sha512sums=('9d8ba8964474e7ed40095e414747a3ce76860ee32b9d8bac19fc7932a79d4bb9947e4cbe810933b6d271f4b0ee5576e25089ca40f414b25faf9245733bfbd369'
            'eb833763f38bfdd3a0c1c36309de68c736b9015e8dd8636e6a6a7dc72acb9aac42298465bfd62abe45622c62374ba495442d75107b3baeec54210158d33cca2c'
            '8f495a5a58714ec1feb7fd7175db44fa48675bd36cd8d77ee881fa055610bf2f7c9cb518057721200725fbec01280c4e7f6363d816c5b5a0f6bb629d2ea71911'
            '960705ff74d25afed477e0b2a5872a3a4fb49ed3105a351f0d0224abc947778f9dbda81e80be94ab636da4a8411a9dd56a8fd4513e5b86a3096a14fa67f1548b'
            'e6ddcab9a6fee024072b6363ef60aa176ed258369bf3a17d475f19b1f410ffd6195b9c5737dc5b1371e8974b44bdbdaa109927acaeb54fb40302a5d67d7c13a8'
            '5da2a4fb0cdaefe9527dec67e01d881161c49a1593f11ddccf28fb55cbc25fe841ea8eca8a50387e2fef5bc304f12a5c9af4909543eafc0f8865dea39f9e8355'
            '8d2518507cf843202dc90c10f6fce3c3fc9b41c7aaf10ff0b862c31d30bbbec7de92f9be0327ea3e58e77e29ae2a12b2f47370502bbddc04324f394cfdd7d4a8')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../0001-seccomp-extend-syscalls-tested-on-Linux-6.1-with-gli.patch
  patch -Np1 < ../0001-tests-enable-drop_privileges-by-default-for-better-s.patch
  # patch -Np1 < ../memcached.service.patch
  sed -e 's/^##safer##//g' -i scripts/*.service
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-seccomp
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm 755 scripts/memcached-tool -t "${pkgdir}/usr/bin"
  install -Dm 644 scripts/memcached-tool.1 -t "${pkgdir}/usr/share/man/man1"

  install -Dm 644 ../*.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 ../memcached.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/memcached.conf"
  install -Dm 644 ../memcached.sysusers "${pkgdir}/usr/lib/sysusers.d/memcached.conf"
}

# vim: ts=2 sw=2 et:
