# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>
# Forked for UBOS to build without --seccomp, see https://github.com/memcached/memcached/issues/444 and https://bugs.archlinux.org/task/61116
# Using our own .service files

pkgverforked=('1.6.9-1')
releasepage=('https://git.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/memcached')

pkgname=memcached
pkgver=1.6.9
pkgrel=1
pkgdesc='Distributed memory object caching system'
url='https://memcached.org/'
arch=('x86_64')
license=('GPL')
depends=('libevent' 'libseccomp' 'libseccomp.so')
optdepends=('perl: for memcached-tool usage')
source=(https://www.memcached.org/files/${pkgname}-${pkgver}.tar.gz
        # memcached.service.patch
        memcached.service  # added
        memcached@.service # added
        memcached.tmpfiles
        memcached.sysusers
        memcached-1.6.8-seccomp-rt_sigreturn.patch)
sha512sums=('2169225aefe6bd7481d919ae3ef95ed85d2ed595f49daceeb13d2dc268097d4aee98cf604824fca103efcdffa7461bb26814209906a5dabf1a5d30af43f6a66c'
            'd133e6751ee77854ec01d6e731cc3cae708552d847cb270f9c8e87aa53b5abb9c47cc83bfae631d752b0ca438669be10053d062c620c65d03d72d9448ba749d7'
            '8f495a5a58714ec1feb7fd7175db44fa48675bd36cd8d77ee881fa055610bf2f7c9cb518057721200725fbec01280c4e7f6363d816c5b5a0f6bb629d2ea71911'
            '960705ff74d25afed477e0b2a5872a3a4fb49ed3105a351f0d0224abc947778f9dbda81e80be94ab636da4a8411a9dd56a8fd4513e5b86a3096a14fa67f1548b'
            'e6ddcab9a6fee024072b6363ef60aa176ed258369bf3a17d475f19b1f410ffd6195b9c5737dc5b1371e8974b44bdbdaa109927acaeb54fb40302a5d67d7c13a8'
            '8fa35ad24b80e5a00d4a3de5625d8dc819d38c53aa5a5c7c43dfddbb4103d27bad08210c11d3415740104766676c5c614de0b317996bb16b55adc471b6dbb73b')

prepare() {
  cd ${pkgname}-${pkgver}
#   patch -Np1 < ../memcached.service.patch
#   sed -e 's/^##safer##//g' -i scripts/*.service

#    # sigignore is deprecated: do not make it an error
#    patch < ../../warning-not-error.patch
  patch -Np1 < ../memcached-1.6.8-seccomp-rt_sigreturn.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-seccomp
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm 755 scripts/memcached-tool -t "${pkgdir}/usr/bin"
  install -Dm 644 scripts/memcached-tool.1 -t "${pkgdir}/usr/share/man/man1"

  install -Dm 644 ../*.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 ../memcached.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/memcached.conf"
  install -Dm 644 ../memcached.sysusers "${pkgdir}/usr/lib/sysusers.d/memcached.conf"
}

# vim: ts=2 sw=2 et:
